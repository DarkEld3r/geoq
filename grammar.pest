WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

number = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

coord = { number ~ number }
coord_seq = { "(" ~ coord ~ ("," ~ coord)* ~ ")" }

point = { ^"POINT" ~ "(" ~ coord ~ ")" }
ls = { ^"LINESTRING" ~ coord_seq }
polygon = { ^"POLYGON" ~ "(" ~ coord_seq+ ~ ")" }
wkt = { (point | ls | polygon) }

base32 = { ASCII_DIGIT | 'b'..'h' | 'j'..'k' | 'm'..'n' | 'p'..'z'}
geohash = { base32+ }

json_boolean = { "true" | "false" }
json_null = { "null" }
json_string = ${ "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}
value = _{ json_object | json_array | json_string | number | json_boolean | json_null }
json_array = {
    "[" ~ "]" |
    "[" ~ value ~ ("," ~ value)* ~ "]"
}
json_object = {
    "{" ~ "}" |
    "{" ~ json_pair ~ ("," ~ json_pair)* ~ "}"
}
json_pair = { json_string ~ ":" ~ value }

latlon = { number ~ "," ~ number }

geoq = _{ SOI ~ (latlon | wkt | geohash | json_object)* ~ EOI }
